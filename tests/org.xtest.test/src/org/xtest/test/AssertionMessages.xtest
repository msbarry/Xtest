runWhileEditing: false
runOnSave: true
import org.^xtest.Xtest
import static org.^xtest.test.TestUtils.*
import static org.^xtest.test.XtestInjector.*

xtest "exceptions" {	
	xtest "not thrown" {
		"assert null throws IllegalArgumentException".assertFailsTo( 
			messageForNoException(IllegalArgumentException::class)
		)
	}
	
	xtest "wrong type" {
		"assert 1/0 throws IllegalArgumentException".assertFailsTo( 
			messageForWrongException(IllegalArgumentException::class, ArithmeticException::class)
		)
	}
}

xtest "simple equals" {
	"assert 1 == 2".assertFailsTo(
		preamble,
		value("1 == 2", false)
	)
}

xtest "enclosed in parenthesis" {
	"assert ((1 == 2))".assertFailsTo(
		preamble,
		value("1 == 2", false)
	)
}

xtest "not operator" {
	"assert !(1 == 1)".assertFailsTo(
		preamble,
		value("!(1 == 1)", false),
		value("1 == 1", true)
	)
}

xtest "resolves variables" {
	"val a = 1; val b = 2; assert a == b".assertFailsTo(
		preamble,
		value("a == b", false),
		value("a", 1),
		value("b", 2)
	)
}

xtest "or operator" {
	"assert 1 == 2 || 2 == 3".assertFailsTo(
		preamble,
		value("1 == 2 || 2 == 3", false),
		value("1 == 2", false),
		value("2 == 3", false)
	)
}

xtest "short circuit and operator" {
	"assert 1 == 2 && 2 == 3".assertFailsTo(
		preamble,
		value("1 == 2 && 2 == 3", false),
		value("1 == 2", false),
		skipped("2 == 3")
	)
}

xtest "line break" {
	"assert 1\n==\n2".assertFailsTo(
		preamble,
		value("1 == 2", false)
	)
}

xtest "block expression" {
	"assert {1 == 2}".assertFailsTo(
		preamble,
		value("1 == 2", false)
	)
}

xtest "if expression" {
	"assert {if (true) false else true}".assertFailsTo(
		preamble,
		value("if (true) false else true", false)
	)
}

xtest "closure skipped" {
	"assert newArrayList(true).map[!it].head".assertFailsTo(
		preamble,
		value("newArrayList(true).map[!it].head", false),
		value("newArrayList(true).map[!it]", newArrayList(true).map[!it]),
		value("newArrayList(true)", newArrayList(true))
	)
}

xtest "handles null" {
	"assert if (true) null else null".assertFailsTo(
		preamble,
		value("if (true) null else null", null)
	)
}

xtest "handles string" {
	'val Object a = "s"; assert switch (a) {String: a == "z"\nInteger: a == 1}'.assertFailsTo(
		preamble,
		value('switch (a) {String: a == "z" Integer: a == 1}', false),
		value('a', 's'),
		value('a == "z"', false),
		value('a', 's')
	)
}

xtest "ignores for" {
	'assert for (i : 1..10) println(i)'.assertFailsTo(
		preamble,
		value('for (i : 1..10) println(i)', null)
	)
}


// Utility methods

def static assertFailsTo(String input, String... expectedMessage) {
	assertValidSyntax(input)
	val result = Xtest::run(input, injector)
	assert result.evaluationException.size == 1
	val compiledExpected = expectedMessage.fold("", [prev,next|prev+next+"\n"])
	assert result.evaluationException.head.cause.message.trim == compiledExpected.trim
}

def static preamble {"Assertion failed"}

def static value(String expr, Object actual) {
	if (actual==null) "   \""+expr+"\" was <null>"
	else "   \""+expr+"\" was "+actual.getClass.simpleName+" <"+actual+">"
}

def static skipped(String expr) {"   \""+expr+"\" was skipped"}

def static messageForNoException(Class<? extends Exception> e) {
	"Expected <"+e.getName+"> but no exception was thrown"
}

def static messageForWrongException(Class<?> expected, Class<?> actual) {
	"Expected <"+expected.getName+"> but threw <"+actual.getName+"> instead"
}